
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath group: "me.seeber.gradle", name: "gradle-project-config", version: "1.0.0"

        if(!project.hasProperty("bootstrap")) {
            classpath group: "me.seeber.gradle", name: "gradle-bintray-config", version: "1.0.0"
            classpath group: "me.seeber.gradle", name: "gradle-github-config", version: "1.0.0"
            classpath group: "me.seeber.gradle", name: "gradle-license-config", version: "1.0.0"
            classpath group: "me.seeber.gradle", name: "gradle-plugin-publish-config", version: "1.0.0"
        }
    }
}

version = "1.0.0"
group = "me.seeber.gradle"
description = "Defaults for the Plugin Publishing Plugin"


if(project.hasProperty("bootstrap")) {
    apply plugin: "groovy"
    apply plugin: "maven"
}
else {
    apply plugin: "me.seeber.distribution.bintray"
    apply plugin: "me.seeber.distribution.gradle-plugin"
    apply plugin: "me.seeber.distribution.maven"
    apply plugin: "me.seeber.ide.eclipse"
    apply plugin: "me.seeber.project.gradle-plugin"
    apply plugin: "me.seeber.repository.github"
    apply plugin: "me.seeber.setup.license"
    apply plugin: "me.seeber.validation.checkstyle"

    model {
        projectConfig {
            organization.name = "Jochen Seeber"
            inceptionYear = 2016
            license.id = "BSD 2-Clause"
        }

        githubConfig { user = "jochenseeber" }
    }

    pluginBundle {
        tags = [
            "gradle",
            "plugin",
            "configuration"
        ]

        plugins { pluginPublishPlugin.id = "me.seeber.project.gradle-plugin" }
    }
}

dependencies {
    compile(group: "me.seeber.gradle", name: "gradle-project-config", version: "1.0.0")
    compile(group: "com.gradle.publish", name: "plugin-publish-plugin", version: "0.9.7")

    if(project.hasProperty("bootstrap")) {
        compile(gradleApi())
        compile(localGroovy())

        compileOnly(group: "org.eclipse.jdt", name: "org.eclipse.jdt.annotation", version: "2.0.0")
    }
    else {
        annotations(group: "me.seeber.annotations", name: "jdk-annotations", version: "1.8.0-r.1")
        annotations(group: "me.seeber.annotations", name: "gradle-api-annotations", version: "3.1-r.1")
    }
}

if(project.hasProperty("bootstrap")) {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2" }
        jcenter()
    }
}
